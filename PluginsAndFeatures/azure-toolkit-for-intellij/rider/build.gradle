dependencies {
    compile rootProject
    compile "org.java-websocket:Java-WebSocket:$web_socket_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"

    testCompile rootProject
}

ext.resharperPluginPath = new File(projectDir, 'ReSharper.Azure')

apply plugin: 'org.jetbrains.intellij'
apply from: 'backend.gradle'

if (!ext.has('BuildNumber'))
    ext.BuildNumber = buildNumber

version "$BuildNumber"
logger.lifecycle("version=$version")
println "##teamcity[buildNumber '$version']"

def extensionsFrom = 'resources/dotnet'

intellij {
    pluginName = 'azure-toolkit-for-rider'
    version = rider_version

    plugins 'terminal', 'restClient', dep_plugins_rider
}

tasks.withType(prepareSandbox.class).configureEach {
    dependsOn buildReSharperPlugin

    from(extensionsFrom, { into "${intellij.pluginName}/dotnet" })
    from(buildReSharperPlugin.outputs, { into "${intellij.pluginName}/dotnet" })
}

buildSearchableOptions.onlyIf { false }

patchPluginXml {
    sinceBuild = patchPluginXmlSinceBuild
}

test {
    dependsOn ':rider:buildPlugin'
    useTestNG()
}

clean {
    dependsOn cleanBackend, ':rider:protocol:cleanProtocolModels'
}

jar {
    dependsOn ':rider:protocol:generateModel'
}
