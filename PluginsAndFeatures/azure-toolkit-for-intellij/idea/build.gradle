import com.microsoft.azuretools.plugins.BundleBuildIDEAPlugin
import org.apache.tools.ant.filters.ReplaceTokens

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

dependencies {
    compile rootProject

    testCompile 'junit:junit:4.12'
    testCompile 'info.cukes:cucumber-junit:1.2.5'
    testCompile 'info.cukes:cucumber-java:1.2.5'
    testCompile 'org.mockito:mockito-core:2.7.22'
    testCompile 'org.assertj:assertj-swing-junit:3.5.0'

    testCompile 'com.github.tomakehurst:wiremock:2.8.0'
    testCompile 'org.powermock:powermock-module-junit4:1.7.0RC4'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.0RC4'
    testCompile 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit"
}

apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: BundleBuildIDEAPlugin

processResources {
    filesMatching('**/ApplicationInsights.xml') {
        filter(ReplaceTokens, tokens: ["applicationinsights.key": project.property("applicationinsights.key")])
    }
}

intellij {
    pluginName = 'azure-toolkit-for-intellij'
    version = idea_version
    updateSinceUntilBuild = Boolean.valueOf(updateVersionRange)
    plugins = idea_version == "IC-2019.1" ? ['maven', dep_plugins, "properties"] : ['java', 'maven', dep_plugins, "properties"]
    downloadSources = Boolean.valueOf(sources)
}

checkstyle {
    toolVersion = '7.7'
    configFile = new File(rootProject.projectDir, 'config/checkstyle/checkstyle.xml')
    showViolations = false
}

findbugs {
    toolVersion = "3.0.1"
    ignoreFailures = true
}

test {
    testLogging.showStandardStreams = true
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task cucumberPackJar(type: Jar) {
    appendix = 'pathing'

    doFirst {
        manifest {
            attributes "Class-Path": configurations.cucumberRuntime.files.collect {
                it.toURI().toString().replaceFirst(/file:\/+/, '/')
            }.join(' ')
        }
    }
}

task cucumber() {
    dependsOn compileTestJava, cucumberPackJar
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = files(sourceSets.main.output, sourceSets.test.output, cucumberPackJar.archivePath)
            args = [
                    '--plugin', 'progress',
                    '--glue', 'com.microsoft.azure.hdinsight.spark.common',
                    '-m',
                    'Test/resources']
        }
    }
}

buildSearchableOptions.onlyIf { false }

test.dependsOn cucumber

installIdea.dependsOn buildPlugin

patchPluginXml {
    sinceBuild = patchPluginXmlSinceBuild
}
